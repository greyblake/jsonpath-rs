dollar = { "$" }
arobase = { "@" }
asterisk = _{ "*" }
dot =  _{ "." }
double_dot =  _{ ".." }

alpha = _{ 'a'..'z' | 'A'..'Z' }
digit = _{ '0'..'9' }
special_charaters = _{ "_" | "-" | "/" | "\\" }
ident = { (alpha | digit | special_charaters)+ }
number = { digit+ }
float = { digit+ ~ "." ~ digit+ }

child = { dot ~ ident }
any_child = { dot ~ asterisk }
indexed_child = { "[" ~ number ~ "]" }
slice = { "[" ~ number ~ ":" ~ number ~ "]" }
slice_to = { "[:" ~ number ~ "]" }
slice_from = { "[" ~ number ~ ":]" }

equal = { "==" }
different = { "!=" }
greater = { ">" }
greater_or_equal = { ">=" }
lower = { "<" }
lower_or_equal = { "<=" }
condition = { equal | different | greater_or_equal | greater | lower_or_equal | lower }

string = { (alpha | digit | special_charaters | " ")+ }
literal = { "'" ~ string ~ "'" }
array = {
    "[" ~
        (" ")* ~
            (
                float ~ ((",") ~ (" ")* ~ float)* |
                number ~ ((",") ~ (" ")* ~ number)* |
                literal ~ ((",") ~ (" ")* ~ literal)*
            ) ~
        (" ")* ~
    "]"
}

sub_expression = { dollar ~ (child | any_child | indexed_child | slice | slice_to | slice_from | filter)* }

and = { "&&" }
or = { "||" }

filter = {
    "[" ~
        "?(" ~
            (dollar | arobase) ~
            (child | any_child | indexed_child | slice | slice_to | slice_from | filter)* ~
            (" ")* ~
            (condition) ~
            (" ")* ~
            (sub_expression | float | number | literal | array) ~
        ")" ~
        (
            (" ")* ~
            (and | or) ~
            (" ")* ~
            "?(" ~
                (dollar | arobase) ~
                (child | any_child | indexed_child | slice | slice_to | slice_from | filter)* ~
                (" ")* ~
                (condition) ~
                (" ")* ~
                ( sub_expression | float | number | literal | array) ~
            ")"
        )* ~
    "]"
}

expression = {
    dollar ~
    (child | any_child | indexed_child | slice | slice_to | slice_from | filter)* ~
    eoi
}
